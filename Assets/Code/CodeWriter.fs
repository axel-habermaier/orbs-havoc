// The MIT License (MIT)
// 
// Copyright (c) 2015, Axel Habermaier
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

namespace Assets

open System
open System.IO
open System.Linq
open System.Text

type internal CodeWriter () =
    let output = StringBuilder ()
    let mutable atBeginningOfLine = true
    let mutable indent = 0

    do output.AppendLine "// Generated by the Point Wars Asset Compiler" |> ignore
    do output.AppendLine () |> ignore

    member public this.AppendFormatted (s, [<ParamArray>] args) =
        this.AddIndentation ()
        output.AppendFormat (s, args) |> ignore

    member public this.AppendFormattedLine (s, [<ParamArray>] args) =
        this.AppendFormatted (s, args)
        this.NewLine ()

    member public this.Append s =
        this.AddIndentation ()
        Printf.bprintf output s

    member public this.AppendLine s =
        Printf.ksprintf (fun s -> this.AppendFormattedLine ("{0}", s)) s

    member public this.NewLine () =
        output.AppendLine () |> ignore
        atBeginningOfLine <- true

    member public this.AppendParenthesized content =
        this.Append "("
        content ()
        this.Append ")"

    member public this.AppendBlockStatement content =
        this.EnsureNewLine ()
        this.AppendLine "{"
        this.IncreaseIndent ()

        content ()

        this.EnsureNewLine ()
        this.DecreaseIndent ()
        this.Append "}"

        this.NewLine ()
        
    member this.AppendBlock content front back =
        this.EnsureNewLine ()
        this.AppendLine front
        this.IncreaseIndent ()
        content ()
        this.EnsureNewLine ()
        this.DecreaseIndent ()
        this.Append back

    member this.AppendRepeated elements content separator =
        let mutable first = true
        for element in elements do
            if not first then
                separator ()
            else
                first <- false
            content element

    member public this.IncreaseIndent() = indent <- indent + 1

    member public this.DecreaseIndent() = indent <- indent - 1

    override this.ToString () =
        output.ToString ()

    member private this.EnsureNewLine() =
        if not atBeginningOfLine then
            this.NewLine ()

    member private this.AddIndentation () =
        if atBeginningOfLine then 
            atBeginningOfLine <- false
            for i = 1 to indent do
                output.Append "    " |> ignore